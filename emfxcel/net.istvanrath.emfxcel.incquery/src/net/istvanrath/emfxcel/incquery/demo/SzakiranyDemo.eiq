package net.istvanrath.emfxcel.incquery.demo

import "http://istvanrath.net/demos/emfxcel"

/*
 * This currently gives a warning, which can be safely ignored.
 * See https://bugs.eclipse.org/bugs/show_bug.cgi?id=418991 for details.
 */
uses net.istvanrath.emfxcel.incquery.lib.*;

@QueryExplorer(display=true, message = "$Nev$ : $NeptunKod$")
pattern AgazatiHallgato(AgazatNeve, Nev, NeptunKod) {
	find NamedColumn("AgazatiBesorolas", "neptunkód", NeptunColumn);
	find CellInWorkSheet(HallgatoNeptun, "AgazatiBesorolas", RowNum, NeptunColumn);
	StringCell.stringValue(HallgatoNeptun, NeptunKod);
	
	find NamedColumn("AgazatiBesorolas", "név", NevColumn);
	find CellInWorkSheet(HallgatoNev, "AgazatiBesorolas", RowNum, NevColumn);
	StringCell.stringValue(HallgatoNev, Nev);

	find NamedColumn("AgazatiBesorolas", "2013/14/2", AgazatColumn);	
	find CellInWorkSheet(Agazat, "AgazatiBesorolas", RowNum, AgazatColumn);
	StringCell.stringValue(Agazat, AgazatNeve);
	check(RowNum>0);
}

@QueryExplorer(display=true, message = "$Nev$ : $NeptunKod$")
@Item(label = "$Nev$", item = HallgatoNev)
pattern OnlabosHallgato(HallgatoNev, Nev, NeptunKod) {
	find NamedColumn("Onlab", "Név", NevColumn);
	find NamedColumn("Onlab", "Neptun kód", NeptunColumn);

	find CellInWorkSheet(HallgatoNev, "Onlab", RowNum, NevColumn);
	find CellInWorkSheet(HallgatoNeptun, "Onlab", RowNum, NeptunColumn);
	
	StringCell.stringValue(HallgatoNev, Nev);
	StringCell.stringValue(HallgatoNeptun, NeptunKod);	
	check(RowNum>0);
}	

@QueryExplorer(display=true, message = "$Nev$ : $NeptunKod$")
pattern IITOnlabosHallgato(Nev, NeptunKod) {
	find NamedColumn("IITOnlab", "Név", NevColumn);
	find NamedColumn("IITOnlab", "Neptun kód", NeptunColumn);

	find CellInWorkSheet(HallgatoNev, "IITOnlab", RowNum, NevColumn);
	find CellInWorkSheet(HallgatoNeptun, "IITOnlab", RowNum, NeptunColumn);
	
	StringCell.stringValue(HallgatoNev, Nev);
	StringCell.stringValue(HallgatoNeptun, NeptunKod);	
	check(RowNum>0);
}	

@QueryExplorer(display=true, message = "$Nev$ : $Neptun$")
pattern NemVetteFelAzOnlabot(Nev, Neptun) {
	neg find OnlabosHallgato(_HC, _HNev, Neptun);
	find AgazatiHallgato("Rendszertervezés", Nev, Neptun);
}

@QueryExplorer(display=true, message = "$Nev$ : $Neptun$")
pattern KakukkFioka(Nev, Neptun) {
	find OnlabosHallgato(_H, Nev, Neptun);
	neg find AgazatiHallgato("Rendszertervezés", _HNev, Neptun);
}

pattern IITreAtSlisszolt(Nev, Neptun) {
	find AgazatiHallgato("Rendszertervezés", Nev, Neptun);
	find IITOnlabosHallgato(_JNev, Neptun);
}

pattern IITKakukkfioka(Nev, Neptun) {
	neg find AgazatiHallgato("Rendszerfejlesztés", _JNev, Neptun);
	find IITOnlabosHallgato(Nev, Neptun);
}

